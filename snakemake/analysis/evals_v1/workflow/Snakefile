"""Snakemake workflow for evaluating genomic language models."""


configfile: "config/config.yaml"


# Helper functions (defined before includes so they're available in all rule files)
def get_all_datasets():
    """Get list of all dataset names."""
    return [d["name"] for d in config["datasets"]]


def get_all_model_steps():
    """Get list of all (model_name, step) combinations."""
    combinations = []
    for model in config["models"]:
        for step in model["steps"]:
            combinations.append((model["name"], step))
    return combinations


def get_custom_plot_config(plot_name):
    """Get configuration for a specific custom plot."""
    if "custom_plots" not in config:
        raise ValueError("No custom_plots defined in config")
    for plot in config["custom_plots"]:
        if plot["name"] == plot_name:
            return plot
    raise ValueError(f"Custom plot {plot_name} not found in config")


def get_all_custom_plot_names():
    """Get list of all custom plot names."""
    if "custom_plots" not in config:
        return []
    return [plot["name"] for plot in config["custom_plots"]]


def get_baseline_model_id(baseline_entry) -> str:
    """Extract the model identifier from a baseline config entry.

    Supports both old format (string) and new format (dict with model/name).
    """
    if isinstance(baseline_entry, dict):
        return baseline_entry["model"]
    return baseline_entry


def get_baseline_display_name(baseline_entry) -> str:
    """Extract the display name from a baseline config entry.

    Supports both old format (string) and new format (dict with model/name).
    """
    if isinstance(baseline_entry, dict):
        return baseline_entry.get("name", baseline_entry["model"])
    return baseline_entry


def get_baseline_input_files(plot_name: str) -> list[str]:
    """Get list of baseline metric file paths needed for a custom plot."""
    plot_config = get_custom_plot_config(plot_name)
    dataset_subsets = plot_config.get("dataset_subsets", [])
    baselines_config = config.get("baselines", {})
    dataset_mapping = baselines_config.get("dataset_mapping", {})
    all_baselines = baselines_config.get("models", [])

    files = []
    for ds in dataset_subsets:
        include_baselines = ds.get("include_baselines", False)
        if not include_baselines:
            continue

        dataset = ds["dataset"]
        if dataset not in dataset_mapping:
            continue

        if isinstance(include_baselines, list):
            baselines_to_include = include_baselines
        else:
            baselines_to_include = all_baselines

        for baseline in baselines_to_include:
            model_id = get_baseline_model_id(baseline)
            files.append(f"results/baselines/metrics/{dataset}/{model_id}.parquet")

    return files


# Include rule files
include: "rules/common.smk"
include: "rules/inference.smk"
include: "rules/metrics.smk"
include: "rules/plots.smk"
include: "rules/baselines.smk"


# Define all output targets
rule all:
    input:
        # All metrics files (cartesian product of datasets and model_steps)
        # [
        #     f"results/metrics/{dataset}/{model}/{step}.parquet"
        #     for dataset in get_all_datasets()
        #     for model, step in get_all_model_steps()
        # ],
        # Plots for each model
        # [
        #     f"results/plots/metrics_vs_step/{model}.svg"
        #     for model in [m["name"] for m in config["models"]]
        # ],
        # # Model comparison plots for each score type
        # [
        #     f"results/plots/models_comparison/{score_type}.svg"
        #     for score_type in config["score_types"]
        # ],
        # Custom filtered comparison plots
        [
            f"results/plots/custom_comparison/{plot_name}.svg"
            for plot_name in get_all_custom_plot_names()
        ],


# Download genome reference
rule download_genome:
    output:
        "results/genome.fa.gz",
    params:
        url=config["genome_url"],
    shell:
        "wget {params.url} -O {output}"
